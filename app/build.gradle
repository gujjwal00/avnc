plugins {
    id 'com.android.application' version '8.10.1'
    id 'org.jetbrains.kotlin.android' version '2.2.10'
    id 'org.jetbrains.kotlin.kapt' version '2.2.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.10'
    id "org.jetbrains.kotlin.plugin.parcelize" version "2.2.10"
}

android {
    namespace 'com.gaurav.avnc'

    compileSdk 35
    ndkVersion "26.1.10909125"

    defaultConfig {
        applicationId "com.gaurav.avnc"
        minSdk 21
        targetSdk 35
        versionCode 46
        versionName "3.0.1"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/roomSchema/".toString()]
            }
        }

        externalNativeBuild {
            cmake {
                //We are not using STL
                arguments '-DANDROID_STL=none'
                targets 'native-vnc', 'vncclient', 'turbojpeg-static', 'wolfssl'
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/roomSchema".toString())
    }

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        ci {
            initWith(release)
            applicationIdSuffix '.ci'
            versionNameSuffix ' (CI)'
        }
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('CMakeLists.txt')
        }
    }

    bundle {
        density {
            enableSplit false
        }
        language {
            enableSplit false
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation "androidx.core:core-ktx:1.15.0"
    implementation 'androidx.activity:activity-ktx:1.9.3'
    implementation "androidx.fragment:fragment-ktx:1.8.6"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.5"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.5"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"

    def roomVersion = "2.7.2"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.google.android.material:material:1.7.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    implementation 'org.connectbot:sshlib:2.2.24'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.6.1"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.6.1"
    androidTestImplementation "androidx.test:rules:1.6.1"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.2.1"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "io.mockk:mockk-android:1.12.0"
}